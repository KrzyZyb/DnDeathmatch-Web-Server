plugins {
	id 'org.springframework.boot' version '2.5.5'
	id 'io.spring.dependency-management' version '1.0.11.RELEASE'
	id 'java'
}
apply plugin: 'war'

group = 'com.dndeathmatch'
version = '0.0.1-SNAPSHOT'
sourceCompatibility = '11'

configurations {
	compileOnly {
		extendsFrom annotationProcessor
	}
}

repositories {
	mavenCentral()
}

dependencies {
	implementation 'org.springframework.boot:spring-boot-starter-security'
	implementation 'org.springframework.boot:spring-boot-starter-web'
	implementation 'org.springframework.boot:spring-boot-starter-websocket'
	implementation 'org.springframework.session:spring-session-core'
	compileOnly 'org.projectlombok:lombok'
	annotationProcessor 'org.projectlombok:lombok'
	testImplementation 'org.springframework.boot:spring-boot-starter-test'
	testImplementation 'org.springframework.security:spring-security-test'
}

test {
	useJUnitPlatform()
}

def webappDir = "../DnDeathmatchUi"

sourceSets {
	main {
		resources {
			srcDirs = ["$projectDir/src/main/resources"]

		}
	}
}

task deleteStaticFolder(type: Delete) {
	def backendStaticDir = "src/main/resources/static"
	def frontendStaticDir = "$webappDir/static"
	file( backendStaticDir ).list().each{
		f ->
			delete "${backendStaticDir}/${f}"
	}
	file(frontendStaticDir).list().each{
		f ->
			delete "${frontendStaticDir}/${f}"
	}
}

task copyFrontend(type: Copy) {
	def frontendStaticDir = "../DnDeathmatchUi/static"
	def backendTargetDir = "./src/main/resources/static"
	from "$frontendStaticDir"
	into "$backendTargetDir"
}

clean{
	dependsOn "deleteStaticFolder"
}

processResources {
	dependsOn "buildAngular"
	dependsOn "copyFrontend"
}

task buildAngular(type:Exec) {
	dependsOn "installAngular"
	workingDir "$webappDir"
	inputs.dir "$webappDir"
	group = BasePlugin.BUILD_GROUP
	if (System.getProperty("os.name").toUpperCase().contains("WINDOWS")){
		commandLine "ng.cmd", "build"
	} else {
		commandLine "ng", "build"
	}
}

task installAngular(type:Exec) {
	workingDir "$webappDir"
	inputs.dir "$webappDir"
	group = BasePlugin.BUILD_GROUP
	if (System.getProperty("os.name").toUpperCase().contains("WINDOWS")){
		commandLine "npm.cmd", "install"
	} else {
		commandLine "npm", "install"
	}
}
